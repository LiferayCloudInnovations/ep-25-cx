def erc = "${project.name}-oauth-application-headless-server"
def generatedFileName = "cx-values.yaml"
def generatedFilePath = layout.buildDirectory.file("tmp/${generatedFileName}").get().asFile

tasks.register('generateValuesYamlFile') {
	outputs.file(generatedFilePath)
	doLast {
		generatedFilePath.getParentFile().mkdirs()
		def clientExtensionConfigContent = layout.buildDirectory.file("liferay-client-extension-build/${project.name}.client-extension-config.json").get().asFile.text.replaceAll(/[\r\n]+/, "").trim()

		def valuesYamlContent = """
clientExtensionConfig:
  configJsons:
    - |
      ${clientExtensionConfigContent}
  kind: Job
  mountInitConfig: true
  setupMainDomainOnCoreDNS: true
  virtualInstanceId: "main.dxp.lfr.dev"
env:
  LIFERAY_BATCH_OAUTH_APP_ERC: ${erc}
image:
  repository: registry:5000/${project.name}
job:
  backoffLimit: 30
"""

		generatedFilePath.write(valuesYamlContent)
		println "Generated values yaml file: ${generatedFilePath}"
	}
}

tasks.named('buildClientExtensionZip', Zip) {
	dependsOn tasks.generateValuesYamlFile

	from(generatedFilePath.getParentFile()) {
		include generatedFileName
	}
	println "Added ${generatedFileName} to buildClientExtensionZip task."
}